<html lang="en">

<!-- Mirrored from mr.gy/ansi-common-lisp/define_002dcompiler_002dmacro.html by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 10 Sep 2021 20:36:04 GMT -->
<head>
<title>define-compiler-macro - ANSI Common Lisp</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="description" content="ANSI Common Lisp">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index-2.html#Top">
<link rel="up" href="Evaluation-and-Compilation.html#Evaluation-and-Compilation" title="Evaluation and Compilation">
<link rel="prev" href="compiler_002dmacro_002dfunction.html#compiler_002dmacro_002dfunction" title="compiler-macro-function">
<link rel="next" href="defmacro.html#defmacro" title="defmacro">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
body {font-family: century schoolbook, serif;
      line-height: 1.3;
      padding-left: 5em; padding-right: 1em;
      padding-bottom: 1em; max-width: 60em;}
table {border-collapse: collapse}
span.roman { font-family: century schoolbook, serif; font-weight: normal; }
h1, h2, h3, h4, h5, h6 {font-family:  Helvetica, sans-serif}
dfn {font-family: inherit; font-variant: italic; font-weight: bolder }
kbd {font-family: monospace; text-decoration: underline}
var {font-family: Helvetica, sans-serif; font-variant: slanted}
td  {padding-right: 1em; padding-left: 1em}
sub {font-size: smaller}
.node {padding: 0; margin: 0}
--></style>
</head>
<body>
<div class="node">
<a name="define-compiler-macro"></a>
<a name="define_002dcompiler_002dmacro"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="defmacro.html#defmacro">defmacro</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="compiler_002dmacro_002dfunction.html#compiler_002dmacro_002dfunction">compiler-macro-function</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Evaluation-and-Compilation.html#Evaluation-and-Compilation">Evaluation and Compilation</a>
<hr>
</div>

<h3 class="heading">define-compiler-macro (Macro)</h3>

<p><a name="index-define_002dcompiler_002dmacro-264"></a><a name="index-define_002dcompiler_002dmacro-265"></a>

<h5 class="subsubheading">Syntax:</h5>

<div class="defun">
&mdash; Macro: <b>define-compiler-macro</b><var> name lambda-list 〚</var><tt>{</tt><var>declaration</var><tt>}</tt><var>* </var><span class="roman">|</span><var> documentation〛 </var><tt>{</tt><var>form</var><tt>}</tt><var>* </var><span class="roman">→</span><var> name<a name="index-define_002dcompiler_002dmacro-266"></a></var><br>
     </div>

<h5 class="subsubheading">Arguments and Values:</h5>

<p><var>name</var>&mdash;a <i>function name</i>.

<p><var>lambda-list</var>&mdash;a <i>macro lambda list</i>.

<p><var>declaration</var>&mdash;a <tt>declare</tt> <i>expression</i>; not evaluated.

<p><var>documentation</var>&mdash;a <i>string</i>; not evaluated.

<p><var>form</var>&mdash;a <i>form</i>.

<h5 class="subsubheading">Description:</h5>

<p>This is the normal mechanism for defining a <i>compiler macro function</i>. 
Its manner of definition is the same as for <code>defmacro</code>; the only
differences are:

     <ul>
<li>The <var>name</var> can be a <i>function name</i> naming
any <i>function</i> or <i>macro</i>.

     <li>The expander function is installed as a <i>compiler macro function</i>
for the <var>name</var>, rather than as a <i>macro function</i>.

     <li>The <code>&amp;whole</code> argument is bound to the form argument that
is passed to the <i>compiler macro function</i>.  The remaining lambda-list
parameters are specified as if this form contained the function name in the
<i>car</i> and the actual arguments in the <i>cdr</i>, but if the <i>car</i>
of the actual form is the symbol <code>funcall</code>, then the destructuring of
the arguments is actually performed using its <i>cddr</i> instead.

     <p><var>Documentation</var> is attached as a <i>documentation string</i>
to <var>name</var> (as kind <code>compiler-macro</code>)
and to the <i>compiler macro function</i>.

     <li>Unlike an ordinary <i>macro</i>, a <i>compiler macro</i>
can decline to provide an expansion merely by returning a form that is
the <i>same</i> as the original (which can be obtained by using
<code>&amp;whole</code>). 
</ul>

<h5 class="subsubheading">Examples:</h5>

<pre class="lisp"> (defun square (x) (expt x 2)) <span class="roman">→</span> SQUARE
 (define-compiler-macro square (&amp;whole form arg)
   (if (atom arg)
       `(expt ,arg 2)
       (case (car arg)
         (square (if (= (length arg) 2)
                     `(expt ,(nth 1 arg) 4)
                     form))
         (expt   (if (= (length arg) 3)
                     (if (numberp (nth 2 arg))
                         `(expt ,(nth 1 arg) ,(* 2 (nth 2 arg)))
                         `(expt ,(nth 1 arg) (* 2 ,(nth 2 arg))))
                     form))
         (otherwise `(expt ,arg 2))))) <span class="roman">→</span> SQUARE
 (square (square 3)) <span class="roman">→</span> 81
 (macroexpand '(square x)) <span class="roman">→</span> (SQUARE X), <i>false</i>
 (funcall (compiler-macro-function 'square) '(square x) nil)
<span class="roman">→</span> (EXPT X 2)
 (funcall (compiler-macro-function 'square) '(square (square x)) nil)
<span class="roman">→</span> (EXPT X 4)
 (funcall (compiler-macro-function 'square) '(funcall #'square x) nil)
<span class="roman">→</span> (EXPT X 2)

 (defun distance-positional (x1 y1 x2 y2)
   (sqrt (+ (expt (- x2 x1) 2) (expt (- y2 y1) 2))))
<span class="roman">→</span> DISTANCE-POSITIONAL
 (defun distance (&amp;key (x1 0) (y1 0) (x2 x1) (y2 y1))
   (distance-positional x1 y1 x2 y2))
<span class="roman">→</span> DISTANCE
 (define-compiler-macro distance (&amp;whole form
                                  &amp;rest key-value-pairs
                                  &amp;key (x1 0  x1-p)
                                       (y1 0  y1-p)
                                       (x2 x1 x2-p)
                                       (y2 y1 y2-p)
                                  &amp;allow-other-keys
                                  &amp;environment env)
   (flet ((key (n) (nth (* n 2) key-value-pairs))
          (arg (n) (nth (1+ (* n 2)) key-value-pairs))
          (simplep (x)
            (let ((expanded-x (macroexpand x env)))
              (or (constantp expanded-x env)
                  (symbolp expanded-x)))))
     (let ((n (/ (length key-value-pairs) 2)))
       (multiple-value-bind (x1s y1s x2s y2s others)
           (loop for (key) on key-value-pairs by #'cddr
                 count (eq key ':x1) into x1s
                 count (eq key ':y1) into y1s
                 count (eq key ':x2) into x2s
                 count (eq key ':y1) into y2s
                 count (not (member key '(:x1 :x2 :y1 :y2)))
                   into others
                 finally (return (values x1s y1s x2s y2s others)))
         (cond ((and (= n 4)
                     (eq (key 0) :x1)
                     (eq (key 1) :y1)
                     (eq (key 2) :x2)
                     (eq (key 3) :y2))
                `(distance-positional ,x1 ,y1 ,x2 ,y2))
               ((and (if x1-p (and (= x1s 1) (simplep x1)) t)
                     (if y1-p (and (= y1s 1) (simplep y1)) t)
                     (if x2-p (and (= x2s 1) (simplep x2)) t)
                     (if y2-p (and (= y2s 1) (simplep y2)) t)
                     (zerop others))
                `(distance-positional ,x1 ,y1 ,x2 ,y2))
               ((and (&lt; x1s 2) (&lt; y1s 2) (&lt; x2s 2) (&lt; y2s 2)
                     (zerop others))
                (let ((temps (loop repeat n collect (gensym))))
                  `(let ,(loop for i below n
                               collect (list (nth i temps) (arg i)))
                     (distance
                       ,@(loop for i below n
                               append (list (key i) (nth i temps)))))))
               (t form))))))
<span class="roman">→</span> DISTANCE
 (dolist (form
           '((distance :x1 (setq x 7) :x2 (decf x) :y1 (decf x) :y2 (decf x))
             (distance :x1 (setq x 7) :y1 (decf x) :x2 (decf x) :y2 (decf x))
             (distance :x1 (setq x 7) :y1 (incf x))
             (distance :x1 (setq x 7) :y1 (incf x) :x1 (incf x))
             (distance :x1 a1 :y1 b1 :x2 a2 :y2 b2)
             (distance :x1 a1 :x2 a2 :y1 b1 :y2 b2)
             (distance :x1 a1 :y1 b1 :z1 c1 :x2 a2 :y2 b2 :z2 c2)))
   (print (funcall (compiler-macro-function 'distance) form nil)))
▷ (LET ((#:G6558 (SETQ X 7))
▷       (#:G6559 (DECF X))
▷       (#:G6560 (DECF X))
▷       (#:G6561 (DECF X)))
▷   (DISTANCE :X1 #:G6558 :X2 #:G6559 :Y1 #:G6560 :Y2 #:G6561))
▷ (DISTANCE-POSITIONAL (SETQ X 7) (DECF X) (DECF X) (DECF X))
▷ (LET ((#:G6567 (SETQ X 7))
▷       (#:G6568 (INCF X)))
▷   (DISTANCE :X1 #:G6567 :Y1 #:G6568))
▷ (DISTANCE :X1 (SETQ X 7) :Y1 (INCF X) :X1 (INCF X))
▷ (DISTANCE-POSITIONAL A1 B1 A2 B2)
▷ (DISTANCE-POSITIONAL A1 B1 A2 B2)
▷ (DISTANCE :X1 A1 :Y1 B1 :Z1 C1 :X2 A2 :Y2 B2 :Z2 C2)
<span class="roman">→</span> NIL
</pre>
<h5 class="subsubheading">See Also:</h5>

<p><a href="compiler_002dmacro_002dfunction.html#compiler_002dmacro_002dfunction">compiler-macro-function</a>,
<a href="defmacro.html#defmacro">defmacro</a>,
<a href="documentation.html#documentation">documentation</a>,
<a href="Syntactic-Interaction-of-Documentation-Strings-and-Declarations.html#Syntactic-Interaction-of-Documentation-Strings-and-Declarations">Section 3.4.11 (Syntactic Interaction of Documentation Strings and Declarations)</a>

<h5 class="subsubheading">Notes:</h5>

<p>The consequences of writing a <i>compiler macro</i> definition for a function
in the <code>COMMON-LISP</code> <i>package</i> are undefined; it is quite possible that in some
<i>implementations</i> such an attempt would override an equivalent or equally
important definition.  In general, it is recommended that a programmer only
write <i>compiler macro</i> definitions for <i>functions</i> he or she personally
maintains&ndash;writing a <i>compiler macro</i> definition for a function maintained
elsewhere is normally considered a violation of traditional rules of modularity
and data abstraction.

</body>
<!-- Mirrored from mr.gy/ansi-common-lisp/define_002dcompiler_002dmacro.html by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 10 Sep 2021 20:36:04 GMT -->
</html>

